/************************************************************************************************************************/
/* Merge Script - Land to source */
/************************************************************************************************************************/

USE [ClaimsBIMetrics]
GO

CREATE OR ALTER PROCEDURE dbo.USPLandThisIsATest
AS

/***********************************************************************************************************************************************
Author: Andrew Logan
Create date: MM/DD/YYYY
Modify date: MM/DD/YYYY
Description: This is the combination of a MERGE INTO script and a UPDATE where differeces script
Purpose: To move data from landing table to source table by adding new recrods, deleting old records, and updating changed records.
Tables: 
    ClaimsBIMetrics.Source.ThisIsATest
    ClaimsBIStage.dbo.ThisIsATest

Reference Tables:  

UDFs:
 
***********************************************************************************************************************************************
**	Change History
***********************************************************************************************************************************************
** Version    Date          Author                       Description
** -------    ----------    -------------------------    --------------------------------------------------------------------------------------
** 1.0	      MM/DD/YYYY    Andrew Logan                 Created USP
***********************************************************************************************************************************************/

    BEGIN

	/*Used to update DeleteDate and ModifiedDatetime values where applicable*/
	DECLARE @dt DATETIME = GETDATE();

	/*
	This MERGE INTO is used to identify additions and deletions only (MATCHED replaces every record not just what changes)
	NOTE: The join can be simple or complex
	*/
	MERGE INTO ClaimsBIMetrics.Source.ThisIsATest AS target
	USING ClaimsBIStage.dbo.LandThisIsATest AS source ON source.ClaimCentury	= target.ClaimCentury  
												     AND source.ClaimYear		= target.ClaimYear
												     AND source.ClaimNbr		= target.ClaimNbr
												     AND source.FeatureSeqNbr	= target.FeatureSeqNbr
	WHEN NOT MATCHED THEN
	INSERT 
	(
		  ClaimCentury
		 ,ClaimYear
		 ,ClaimNbr
		 ,FeatureSeqNbr
		 ,Field1
		 ,Field2
		 ,Field3
	)
	VALUES
	(
		  source.ClaimCentury
		 ,source.ClaimYear
		 ,source.ClaimNbr
		 ,source.FeatureSeqNbr
		 ,source.Field1
		 ,source.Field2
		 ,source.Field3
	)
	WHEN NOT MATCHED BY source THEN 
	UPDATE SET
		/*The metric data warehouse does not actually delete records it just adds a delete date*/
		target.DeleteDate = COALESCE(target.DeleteDate,@dt) 
	;

	/*This UPDATE is used to identify records that changed and only update those records*/
	UPDATE target
	SET 
		 target.Field1				= source.Field1
		,target.Field2				= source.Field2
		,target.Field3				= source.Field3
		,target.ModifiedDateTime	= @dt /*NOT NEEDED IF INCREMENTAL LOAD OR TEMPORAL TABLE*/

	FROM
		ClaimsBIMetrics.Source.ThisIsATest AS target
		INNER JOIN ClaimsBIStage.dbo.LandThisIsATest AS source ON source.ClaimCentury	= target.ClaimCentury  
															  AND source.ClaimYear		= target.ClaimYear
															  AND source.ClaimNbr		= target.ClaimNbr
															  AND source.FeatureSeqNbr	= target.FeatureSeqNbr

	/*This compares each field in a row and returns any rows where at least one value changed (join fields are removed)*/
	WHERE EXISTS
	(
		SELECT
			  target.Field1
			 ,target.Field2
			 ,target.Field3

		EXCEPT

		SELECT
			  source.Field1
			 ,source.Field2
			 ,source.Field3
	);

	END;
GO

/************************************************************************************************************************/
/* Merge Script - source to Dimension */
/************************************************************************************************************************/

USE ClaimsBIMetrics
GO

CREATE OR ALTER PROCEDURE dbo.USPThisIsATest
AS

/***********************************************************************************************************************************************
Author: Andrew Logan
Create date: MM/DD/YYYY
Modify date: MM/DD/YYYY
Description: 
Purpose: To move data from landing table to source table by adding new recrods, deleting old records, and updating changed records.
Tables: 
    ClaimsBIMetrics.dbo.ThisIsATest
    ClaimsBIMetrics.Source.ThisIsATest

Reference Tables:  

UDFs:
 
***********************************************************************************************************************************************
**	Change History
***********************************************************************************************************************************************
** Version    Date          Author                       Description
** -------    ----------    -------------------------    --------------------------------------------------------------------------------------
** 1.0	      MM/DD/YYYY    Andrew Logan                 Created USP
***********************************************************************************************************************************************/

    BEGIN

	/*Used to update DeleteDate and ModifiedDatetime values where applicable*/
	DECLARE @dt DATETIME = GETDATE();

	/*
	This MERGE INTO is used to identify additions and deletions only (MATCHED replaces every record not just what changes)
	NOTE: The join can be simple or complex
	*/
	MERGE INTO ClaimsBIMetrics.dbo.DimThisIsATest AS target
	USING ClaimsBIMetrics.source.ThisIsATest AS source ON source.ClaimCentury	= target.ClaimCentury  
												      AND source.ClaimYear		= target.ClaimYear
												      AND source.ClaimNbr		= target.ClaimNbr
												      AND source.FeatureSeqNbr	= target.FeatureSeqNbr
	WHEN NOT MATCHED THEN
	INSERT 
	(
		  ClaimCentury
		 ,ClaimYear
		 ,ClaimNbr
		 ,FeatureSeqNbr
		 ,Field1
		 ,Field2
		 ,Field3
	)
	VALUES
	(
		  source.ClaimCentury
		 ,source.ClaimYear
		 ,source.ClaimNbr
		 ,source.FeatureSeqNbr
		 ,source.Field1
		 ,source.Field2
		 ,source.Field3
	)
	WHEN NOT MATCHED BY source THEN 
	UPDATE SET
		/*The metric data warehouse does not actually delete records it just adds a delete date*/
		target.DeleteDate = COALESCE(target.DeleteDate,@dt) 
	;

	/*This UPDATE is used to identify records that changed and only update those records*/
	UPDATE target
	SET 
		 target.Field1				= source.Field1
		,target.Field2				= source.Field2
		,target.Field3				= source.Field3
		--,target.ModifiedDatetime	= @dt /*NOT NEEDED IF INCREMENTAL LOAD OR TEMPORAL TABLE*/

	FROM
		ClaimsBIMetrics.dbo.DimThisIsATest AS target
		INNER JOIN ClaimsBIMetrics.source.ThisIsATest AS source ON source.ClaimCentury	= target.ClaimCentury  
															   AND source.ClaimYear		= target.ClaimYear
															   AND source.ClaimNbr		= target.ClaimNbr
															   AND source.FeatureSeqNbr	= target.FeatureSeqNbr

	/*This compares each field in a row and returns any rows where at least one value changed (join fields are removed)*/
	WHERE EXISTS
	(
		SELECT
			  target.Field1
			 ,target.Field2
			 ,target.Field3

		EXCEPT

		SELECT
			  source.Field1
			 ,source.Field2
			 ,source.Field3
	);

	END;
GO



